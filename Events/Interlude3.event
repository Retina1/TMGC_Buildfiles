//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

EventPointerTable(EventsI3,ThisChapter)


ALIGN 4

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN I3Units I3Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0x0,I3Open,1)
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
CharacterEvent(0x0,I3ReadyToEnd,Blair,Cid)
CharacterEvent(0x8,I3ArinTalk,Blair,Arin)
CharacterEvent(0x9,I3KennethTalk,Blair,Kenneth)
CharacterEvent(0xa,I3NatasiaTalk,Blair,Natasia)
CharacterEvent(0xb,I3StefanTalk,Blair,Stefan)
CharacterEvent(0xc,I3DesmondTalk,Blair,Desmond)
CharacterEvent(0xd,I3SamTalk,Blair,Sam)
CharacterEvent(0xe,I3JosefTalk,Blair,Josef)
CharacterEvent(0xf,I3PeggyTalk,Blair,Peggy)
CharacterEvent(0x10,I3ButchTalk,Blair,Butch)
CharacterEvent(0x11,I3CassiusTalk,Blair,Cassius)
CharacterEvent(0x12,I3RachaelTalk,Blair,Rachael)
CharacterEvent(0x13,I3JeffTalk,Blair,Jeff)
CharacterEvent(0x14,I3EmileTalk,Blair,Emile)
CharacterEvent(0x15,I3ApolloTalk,Blair,Apollo)
CharacterEvent(0x16,I3BechamelTalk,Blair,Bechamel)
END_MAIN

LocationBasedEvents:
Armory(I3Armory,5,19)
Vendor(I3Vendor,8,19)
END_MAIN

ALIGN 4
I3Armory:
SHLI IronSword SteelSword IronLance SteelLance IronAxe SteelAxe IronBow SteelBow
BYTE 0x0 0x0
ALIGN 4
I3Vendor:
SHLI Fire Thunder Lightning Shine Flux Nightfall Heal Mend Vulnerary
BYTE 0x0 0x0
ALIGN 4

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4

I3Units:
UNIT Blair BlairLord 0x0 Level(1,Ally,0) [10,15] 0x0 0x0 0x0 0x0 [Fire, Awaken, Vulnerary] NoAI
UNIT

I3Autorecruits:
UNIT Arin 0x0 0x0 Level(1,Ally,0) [5,8] 0x0 0x0 0x1 REDA3R9 [0x0] NoAI
UNIT Kenneth 0x0 0x0 Level(1,Ally,0) [5,8] 0x0 0x0 0x1 REDA7R11 [0x0] NoAI
UNIT Natasia 0x0 0x0 Level(1,Ally,0) [5,8] 0x0 0x0 0x1 REDA9R10 [0x0] NoAI
UNIT Stefan 0x0 0x0 Level(1,Ally,0) [8,8] 0x0 0x0 0x1 REDA11R17 [0x0] NoAI
UNIT Desmond 0x0 0x0 Level(1,Ally,0) [8,8] 0x0 0x0 0x1 REDA11R16 [0x0] NoAI
UNIT Sam 0x0 0x0 Level(1,Ally,0) [8,8] 0x0 0x0 0x1 REDA9R5 [0x0] NoAI
UNIT Peggy 0x0 0x0 Level(1,Ally,0) [4,14] 0x0 0x0 0x1 REDA2R16 [0x0] NoAI
UNIT Rachael 0x0 0x0 Level(1,Ally,0) [9,14] 0x0 0x0 0x1 REDA5R17 [0x0] NoAI
UNIT Cassius 0x0 0x0 Level(1,Ally,0) [9,14] 0x0 0x0 0x1 REDA5R5 [0x0] NoAI
UNIT

//others done sep. since autochecking dead/alive status doesn't work if unrecruited
I3Josef:
UNIT Josef 0x0 0x0 Level(1,Ally,0) [8,8] 0x0 0x0 0x1 REDA7R14 [0x0] NoAI
UNIT

I3Butch:
UNIT Butch 0x0 0x0 Level(1,Ally,0) [4,14] 0x0 0x0 0x1 REDA2R17 [0x0] NoAI
UNIT

I3Emile:
UNIT Emile 0x0 0x0 Level(1,Ally,0) [5,8] 0x0 0x0 0x1 REDA2R11 [0x0] NoAI
UNIT

I3Jeff:
UNIT Jeff 0x0 0x0 Level(1,Ally,0) [4,14] 0x0 0x0 0x1 REDA2R20 [0x0] NoAI
UNIT

I3Apollo:
UNIT Apollo 0x0 0x0 Level(1,Ally,0) [9,14] 0x0 0x0 0x1 REDA11R20 [0x0] NoAI
UNIT

I3Bechamel:
UNIT Bechamel 0x0 0x0 Level(1,Ally,0) [9,14] 0x0 0x0 0x1 REDA7R3 [0x0] NoAI
UNIT

I3NPC:
UNIT Cid Berserker 0x0 Level(9,NPC,0) [6,3] 0x0 0x0 0x0 0x0 [Frypan,Elixir] NeverMoveAI
UNIT

ALIGN 4

BeginningScene:
EnableFreeMovement
LOAD1 0x0 I3Units
LOAD1 0x0 I3NPC
ENUN
MUSC NarrationTheme
Narration(Base3_Narration)
CleanTextNoMap(BoatCG,I3_Opening)
GotoPrepScreen
ENDA

EndingScene:
DisableFreeMovement
EVBIT_MODIFY 0
//maybe change cg or music here- contemplative about journey so far into oh shit the yg'strans (or maybe keep yg'strans for chapter 10)
Text(I3ReleaseMe)
MUSC S_RightRoad
CleanTextNoMap(BoatCG,I3Ending)
MoveToChapter(Chapter10)
ENDA

I3Open:
CamOn
LOAD1 0x0 I3Autorecruits
CHECK_ALIVE Josef
BEQ 0x1 0xc 0x0
LOAD1 0x0 I3Josef
LABEL 0x1
CHECK_ALIVE Butch
BEQ 0x2 0xc 0x0
LOAD1 0x0 I3Butch
LABEL 0x2
CHECK_ALIVE Emile
BEQ 0x3 0xc 0x0
LOAD1 0x0 I3Emile
LABEL 0x3
CHECK_ALIVE Jeff
BEQ 0x4 0xc 0x0
LOAD1 0x0 I3Jeff
LABEL 0x4
CHECK_ALIVE Apollo
BEQ 0x5 0xc 0x0
LOAD1 0x0 I3Apollo
LABEL 0x5
CHECK_ALIVE Bechamel
BEQ 0x6 0xc 0x0
LOAD1 0x0 I3Bechamel
LABEL 0x6
ENUN
CamOff
EndEvent

I3ReadyToEnd:
EVBIT_MODIFY 4
Text(I3ConfirmLeave)
SystemText(I1LeavePrompt)
IfNoSelectedGoto(0x2)
CALL EndingScene
GOTO 0x1
LABEL 0x2
Text(I3NotYet)
LABEL 0x1
EndEvent

I3ArinTalk:
Text(I3ArinConvo)
EndEvent

I3KennethTalk:
Text(I3KennethConvo)
AddUnitStatus(Kenneth,Defence,1)
SystemText(I3KennethDefUp)
EndEvent

I3NatasiaTalk:
Text(I3NatasiaConvo)
AddUnitStatus(Natasia,Resistance,2)
SystemText(I3NatasiaResUp)
EndEvent

I3StefanTalk:
Text(I3StefanConvo)
AddUnitStatus(Stefan,Speed,2)
SystemText(I3StefanSpeedUp)
EndEvent

I3DesmondTalk:
Text(I3DesmondConvo)
EndEvent

I3SamTalk:
Text(I3SamConvo)
EndEvent

I3JosefTalk:
Text(I3JosefConvo)
EndEvent

I3PeggyTalk:
Text(I3PeggyConvo)
AddUnitStatus(Peggy,Strength,2)
SystemText(I3PeggyStrengthUp)
EndEvent

I3ButchTalk:
Text(I3ButchConvo)
EndEvent

I3CassiusTalk:
Text(I3CassiusConvo)
AddUnitStatus(Cassius,Skill,1)
SystemText(I3CassiusSkillUp)
EndEvent

I3RachaelTalk:
Text(I3RachaelConvo)
AddUnitStatus(Rachael,MaxHP,3)
SystemText(I3RachaelHPUp)
EndEvent

I3JeffTalk:
Text(I3JeffConvo)
AddUnitStatus(Jeff,LuckStat,2)
SystemText(I3JeffLuckUp)
EndEvent

I3EmileTalk:
Text(I3EmileConvo)
EndEvent

I3ApolloTalk:
Text(I3ApolloConvo)
GiveItem(ApolloNote,Blair)
EndEvent

I3BechamelTalk:
Text(I3BechamelConvo)
EndEvent

ALIGN 4

MESSAGE Events end at offset CURRENTOFFSET