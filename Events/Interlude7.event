//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

EventPointerTable(EventsI7,ThisChapter)


ALIGN 4

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN I7Units I7Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0x0,I7MapStart,1)
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
CharacterEvent(0x0,I7ReadyToEnd,Viridian,Freddric)
CharacterEvent(0x10,I7ViridianFamily,Viridian,I7Valeria)
CharacterEvent(0x10,I7ViridianFamily,Viridian,Victor)
CharacterEvent(0x11,I7CouncilMembers,Viridian,Elcorian)
CharacterEvent(0x11,I7CouncilMembers,Viridian,0xa4)
CharacterEvent(0x12,I7KennethDad,Viridian,0xa3)
CharacterEvent(0x13,I7Joania,Viridian,Joan)
CharacterEvent(0x13,I7Joania,Viridian,Sonia)
CharacterEvent(0xe,I7Merchant,Viridian,Merchant)
END_MAIN

LocationBasedEvents:
Armory(I7Armory,6,10)
Armory(I7Armory2,16,10)
Vendor(I7Vendor,1,10)
Vendor(I7Vendor2,21,10)
House(0x1a,I7House1,21,3)
House(TavernI7Flag,I7House2,4,10)
House(0x1c,I7House3,1,21)
House(0x1d,I7House4,20,26)
END_MAIN

ALIGN 4
I7Armory:
SHLI IronSword SteelSword SilverSword Lancereaver Armorslayer Zanbato KillingEdge IronAxe SteelAxe SilverAxe Swordreaver Hammer Halberd KillerAxe HandAxe
BYTE 0x0 0x0
ALIGN 4
I7Armory2:
SHLI IronLance SteelLance SilverLance Axereaver Shatterspear Horseslayer KillerLance Javelin IronBow SteelBow SilverBow KillerBow Longbow ShiningBow Wardbow
BYTE 0x0 0x0
ALIGN 4
I7Vendor:
SHLI Fire Thunder Elfire Inversion Calibur Lightning Shine Divine Banish Pierce Flux Nightfall Nosferatu Corruption Knoggelvi
BYTE 0x0 0x0
ALIGN 4
I7Vendor2:
SHLI Heal Mend Recover Physic Restore Barrier Yoink Rewarp Vulnerary Elixir PureWater
BYTE 0x0 0x0
ALIGN 4


MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4

I7Units:
UNIT Viridian BlairLord 0x0 Level(1,Ally,0) [11,12] 0x0 0x0 0x0 0x0 [Fire, Awaken, Vulnerary] NoAI
UNIT Freddric WyvLord 0x0 Level(1,Ally,0) [11,11] 0x0 0x0 0x0 0x0 [Fire, Awaken, Vulnerary] NoAI
UNIT

I7NPC:
UNIT I7Valeria CitizenF 0x0 Level(16,NPC,0) [17,12] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Victor CitizenM 0x0 Level(14,NPC,0) [19,12] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Merchant SwMast 0x0 Level(20,NPC,1) [18,1] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Dalst Falco 0x0 Level(17,NPC,1) [8,17] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Dalst Warrior 0x0 Level(18,NPC,1) [4,2] 0x0 0x0 0x0 0x0 [Swordreaver] NeverMoveAI
UNIT Dalst Hero 0x0 Level(20,Enemy,1) [2,2] 0x0 0x0 0x0 0x0 [BraveSword] NeverMoveAI
UNIT Dalst CitizenF 0x0 Level(4,NPC,1) [2,6] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Dalst CitizenM 0x0 Level(4,NPC,1) [4,6] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Dalst Sage 0x0 Level(15,NPC,1) [5,27] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Dalst Merc 0x0 Level(8,NPC,1) [12,23] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Dalst DarkF 0x0 Level(13,NPC,1) [22,21] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Dalst AxeGen 0x0 Level(20,NPC,1) [8,1] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Dalst MageF 0x0 Level(1,NPC,1) [17,4] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Dalst Bishop 0x0 Level(6,NPC,1) [2,12] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Joan HeroF 0x0 Level(11,Enemy,1) [10,2] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Sonia HalberdierF 0x0 Level(13,Enemy,1) [10,3] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Elcorian Valk 0x0 Level(16,NPC,0) [3,16] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT 0xa4 SageF 0x0 Level(18,NPC,0) [4,16] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT 0xa3 WyvLord 0x0 Level(17,NPC,0) [15,18] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
//add flavor npcs - classes having mock battles on stage, various citizens to talk
UNIT

ALIGN 4

BeginningScene:
EnableFreeMovement
MUSC NarrationTheme4
Narration(Map2CG,Base7_Narration)
CleanTextNoMap(StreetCG,I7Opening)
LOAD1 0x0 I7NPC
ENUN
LOAD1 0x0 I7Units
ENUN
GotoPrepScreenNoBaseCheck
ENDA

EndingScene:
DisableFreeMovement
EVBIT_MODIFY 3
Text(I7ReleaseMe)
LocationText(HouseCG,I7DinnerScene,ArbordiaInnLabel,S_WelcomeToLegend)
CHECK_EVENTID 0x12
BEQ 0x1 0xc 0x0
LocationText(HallCG, I7TheNextJobTarquinKnown,ArbordiaMeetingHallLabel,S_RigorousJustice)
GOTO 0x2
LABEL 0x1
LocationText(HallCG, I7TheNextJob,ArbordiaMeetingHallLabel,S_RigorousJustice)
LABEL 0x2
LocationText(HouseCG,I7WrapUp,ArbordiaInnLabel,0x0)
MoveToChapter(ChapterV3)
ENDA

I7ReadyToEnd:
CHECK_EVENTID 0x10
BNE 0x6 0xc 0x0
Text(I7TalkToYourFamilyGoober)//talk to your family please
GOTO 0x1
LABEL 0x6
EVBIT_MODIFY 4
Text(I7ConfirmLeave)
SystemText(I1LeavePrompt)
IfNoSelectedGoto(0x2)
CALL EndingScene
GOTO 0x1
LABEL 0x2
Text(I7NotYet)//not yet
LABEL 0x1
EndEvent

I7Merchant:
EVBIT_MODIFY 4
Text(I7MerchantTalk)
CHECK_MONEY
SVAL 0x1 0x5DC
BGE 0x1 0xc 0x1
Text(I7NoMoneyHa)
ENUF 0xe
GOTO 0x2
LABEL 0x1
SystemText(I7ScrollBuy)
IfNoSelectedGoto(0x3)
Text(I7ScrollPurchased)
SVAL 0x3 0x5dc
TAKEMONEY
GiveItem(ArmsScroll,Viridian)
GOTO 0x2
LABEL 0x3
Text(I7ScrollDenied)
ENUF 0xe
LABEL 0x2
EndEvent

I7ViridianFamily:
MUSC S_CircleOfTheMoon
Text(I7FamilyConvo)
EndEvent

I7CouncilMembers:
MUSC S_Chaos
Text(I7ElcorianEthylConvo)
EndEvent

I7KennethDad:
MUSC S_RigorousJustice
Text(I7TarquinConvo)
EndEvent

I7Joania:
Text(I7JoanSoniaConvo)
EndEvent

I7House1:
HouseEvent(I7House1Text,House2CG)
I7House2:
HouseEvent(I7InnText,InnCG)
I7House3:
HouseEvent(I7House2Text,House2CG)
I7House4:
HouseEvent(I7House3Text,House2CG)

I7MapStart:
Text(I7Gold)//after paying merc salaries and setting aside family money, viridian has 60k left for equipment and other expenses
GiveMoney(0xEA60)
EndEvent


//MESSAGE Events end at offset CURRENTOFFSET