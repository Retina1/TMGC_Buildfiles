//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

EventPointerTable(EventsI5,ThisChapter)


ALIGN 4

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN I5Units I5Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0x0,I5MapStart,1)
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
CharacterEvent(0x0,I5ReadyToEnd,Blair,Brandl)
CharacterEvent(0x13,I5Zeke,Blair,Zeke)
CharacterEvent(0x14,I5Hyperius,Blair,Hyperius)
CharacterEvent(0x15,I5Argent,Blair,Argent)
CharacterEvent(0x16,I5ViridFred,Blair,Viridian)
CharacterEvent(0x16,I5ViridFred,Blair,Freddric)
CharacterEvent(0x11,I5Merchant,Blair,Merchant)//sell item
CharacterEvent(0x12,I5Merchant2,Blair,Merchant3)//sell a stat boost
END_MAIN

LocationBasedEvents:
Armory(I5Armory,15,10)
Vendor(I5Vendor,18,10)
House(TavernI5Flag,I5House1,12,10)
House(0x1b,I5House2,21,14)
House(0x1c,I5House3,4,20)
House(0x1d,I5House4,7,20)
House(0x1e,I5House5,0,28)
House(0x1f,I5House6,11,32)
House(0x21,I5House7,20,32)
House(0x22,I5House8,22,32)
END_MAIN

ALIGN 4
I5Armory:
SHLI SteelSword SilverSword Lancereaver LightBrand SteelLance SilverLance Javelin Axereaver SteelAxe SilverAxe HandAxe Swordreaver SteelBow SilverBow Longbow ShiningBow
BYTE 0x0 0x0
ALIGN 4
I5Vendor:
SHLI Thunder Elfire Fimbulvetr Shine Divine Aura Nightfall Nosferatu Fenrir Mend Recover Vulnerary Elixir PureWater ChestKey DoorKey
BYTE 0x0 0x0
ALIGN 4

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4

I5Units:
UNIT Blair BlairLord 0x0 Level(1,Ally,0) [11,37] 0x0 0x0 0x0 0x0 [Fire, Awaken, Vulnerary] NoAI
UNIT Zeke 0x0 0x0 Level(1,Ally,0) [3,1] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Hyperius 0x0 0x0 Level(1,Ally,0) [1,2] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT

OhHaiElcorian:
UNIT Elcorian Valk 0x0 Level(1,NPC,0) [4,0] 0x0 0x0 0x1 REDA4R2 [0x0] NoAI
UNIT

I5NPC:
UNIT Brandl Sniper 0x0 Level(11,NPC,0) [4,1] 0x0 0x0 0x0 0x0 [Longbow, Elixir] NeverMoveAI
UNIT Argent BowGen 0x0 Level(14,NPC,0) [12,2] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Viridian ViridianLord 0x0 Level(4,NPC,0) [11,12] 0x0 0x0 0x0 0x0 [Luckbow] NeverMoveAI
UNIT Freddric WyvLord 0x0 Level(5,NPC,0) [13,11] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Merchant ValkF 0x0 Level(15,NPC,1) [0,34] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Merchant3 Bishop 0x0 Level(13,NPC,1) [3,13] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT ElbaCor MercF 0x0 Level(19,NPC,1) [15,31] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT ElbaCor SageF 0x0 Level(2,NPC,1) [6,30] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT ElbaCor Falco 0x0 Level(9,NPC,1) [25,1] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT ElbaCor LncGen 0x0 Level(19,NPC,1) [2,0] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT ElbaCor AxeGen 0x0 Level(18,NPC,1) [5,0] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT ElbaCor SwdPeg 0x0 Level(12,NPC,1) [13,17] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT ElbaCor Soldier 0x0 Level(14,NPC,1) [7,9] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT ElbaCor SoldierF 0x0 Level(11,NPC,1) [8,11] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT ElbaCor Hero 0x0 Level(3,NPC,1) [1,22] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT ElbaCor Cleric 0x0 Level(4,NPC,1) [15,37] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Bandit Berserker 0x0 Level(14,Enemy,1) [29,5] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Bandit Berserker 0x0 Level(14,Enemy,1) [27,31] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT

ALIGN 4

BeginningScene:
EnableFreeMovement
LOAD1 0x0 I5Units
LOAD1 0x0 I5NPC
ENUN
MUSC NarrationTheme3
Narration(MapCG,Base5_Narration)
MUSC AmbientBirds
CleanTextNoMap(StreetCG,I5Opening)//blair returns after zeke
GotoPrepScreenNoBaseCheck
ENDA

EndingScene:
DisableFreeMovement
EVBIT_MODIFY 3
Text(I5FreeMe)//blair talks to king, meets zeke
Reinforcements(OhHaiElcorian)
Text(I5ElcorianGreeting)
LocationText(ElbaCorCastleCG, I5Ending2,ElbaCorCastleLabel,S_RigorousJustice)
SetBackground(BlankCG)
GiveItem(ResoluteSeal,Blair)
CHECK_ALIVE Kenneth
BEQ 0x1 0xc 0x0
CleanTextNoMap(ElbaCorCastleCG,I5KennethScene)//kenneth says hi to dad
LABEL 0x1
CHECK_EVENTID 0x16
BNE 0x2 0xc 0x0
LocationText(VillageCG,I5ViridFredPostmapText,CouriaLabel,S_TMSQuestBattle)
SetBackground(BlankCG)
GiveItem(MercFlier,Blair)
LABEL 0x2
MoveToChapter(Chapter15)
ENDA

I5MapStart:
Text(I5StartText)
GiveMoney(0x2710)
EndEvent

I5Zeke:
MUSC S_CircleOfTheMoon
Text(I5ZekeText)
EndEvent
I5Hyperius:
Text(I5HyperiusText)
EndEvent
I5Argent:
Text(I5ArgentText)
EndEvent
I5ViridFred:
Text(I5ViridFredText)
GiveItem(MercFlier,Blair)
EndEvent


I5House1:
HouseEvent(I5InnText,InnCG)
I5House2:
HouseEvent(I5House2Text,HouseCG)
I5House3:
HouseEvent(I5House3Text,HouseCG)
I5House4:
HouseEvent(I5House4Text,House2CG)
I5House5:
HouseEvent(I5House5Text,HouseCG)
I5House6:
HouseEvent(I5House6Text,House2CG)
I5House7:
MUSC S_Ameno
SMOV 2 House2CG
SMOV 3 I5House7Text
CALL $9EE310
EVBIT_T 0x7
ENDA
I5House8:
HouseEvent(I5House8Text,HouseCG)

I5Merchant:
EVBIT_MODIFY 4
Text(I5MerchantTalk)
CHECK_MONEY
SVAL 0x1 0x640
BGE 0x1 0xc 0x1
Text(I5NoMoneyHa)
ENUF 0x15
GOTO 0x2
LABEL 0x1
SystemText(I5ItemBuy)
IfNoSelectedGoto(0x3)
Text(I5ItemPurchased)
SVAL 0x3 0x640
TAKEMONEY
GiveItem(LiveGoose,Blair)
GOTO 0x2
LABEL 0x3
Text(I5ItemDenied)
ENUF 0x11
LABEL 0x2
EndEvent

I5Merchant2:
EVBIT_MODIFY 4
Text(I5Merchant2Talk)
CHECK_MONEY
SVAL 0x1 0xbb8
BGE 0x1 0xc 0x1
Text(I5NoMoneyHa2)
ENUF 0x15
GOTO 0x2
LABEL 0x1
SystemText(I5StatBuy)
IfNoSelectedGoto(0x3)
Text(I5StatPurchased)
SVAL 0x3 0xbb8
TAKEMONEY
AddUnitStatus(Blair, MaxHP, 1)
AddUnitStatus(Blair, CurrentHP, 1)
AddUnitStatus(Blair, Strength, 1)
AddUnitStatus(Blair, Magic, 1)
AddUnitStatus(Blair, Skill, 1)
AddUnitStatus(Blair, Speed, 1)
AddUnitStatus(Blair, LuckStat, 1)
AddUnitStatus(Blair, Defence, 1)
AddUnitStatus(Blair, Resistance, 1)
SystemText(I5BlairStatsUp)
GOTO 0x2
LABEL 0x3
Text(I5StatDenied)
ENUF 0x12
LABEL 0x2
EndEvent

I5ReadyToEnd:
EVBIT_MODIFY 4
Text(I5ReadyText)
SystemText(I1LeavePrompt)
IfNoSelectedGoto(0x2)
CALL EndingScene
GOTO 0x1
LABEL 0x2
Text(I5NotYet)
LABEL 0x1
EndEvent

//MESSAGE Events end at offset CURRENTOFFSET