//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

EventPointerTable(EventsI5,ThisChapter)


ALIGN 4

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN I5Units I5Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
CharacterEvent(0x0,I5ReadyToEnd,Blair,Brandl)
CharacterEvent(0x11,I5Merchant,Blair,Merchant)
END_MAIN

LocationBasedEvents:
Armory(I5Armory,1,24)
Vendor(I5Vendor,11,24)
END_MAIN

ALIGN 4
I5Armory:
SHLI SteelSword SilverSword Lancereaver LightBrand SteelLance SilverLance Javelin Axereaver SteelAxe SilverAxe HandAxe Swordreaver SteelBow SilverBow Longbow ShiningBow
BYTE 0x0 0x0
ALIGN 4
I5Vendor:
SHLI Thunder Elfire Shine Divine Nightfall Nosferatu Mend Recover Vulnerary Elixir PureWater ChestKey DoorKey
BYTE 0x0 0x0
ALIGN 4

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4

I5Units:
UNIT Blair BlairLord 0x0 Level(1,Ally,0) [5,1] 0x0 0x0 0x0 0x0 [Fire, Awaken, Vulnerary] NoAI
UNIT

I5NPC:
UNIT Brandl Sniper 0x0 Level(15,NPC,0) [0,0] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Argent BowGen 0x0 Level(4,NPC,1) [0,1] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Viridian ViridianLord 0x0 Level(14,NPC,0) [0,2] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Freddric WyvLord 0x0 Level(12,NPC,0) [0,3] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Merchant SorcF 0x0 Level(4,NPC,0) [0,4] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT

ALIGN 4

BeginningScene:
EnableFreeMovement
LOAD1 0x0 I5Units
LOAD1 0x0 I5NPC
ENUN
CleanTextNoMap(HallCG,FillerDialogue)//negotiations, dalst yoinks ygstra
MUSC NarrationTheme2
Narration(Map2CG,Base5_Narration)
CleanTextNoMap(HallCG,FillerDialogue)//zeke returns
GotoPrepScreenNoBaseCheck
ENDA

EndingScene:
DisableFreeMovement
Text(FillerDialogue)
MoveToChapter(Chapter15)
ENDA

I5Merchant:
EVBIT_MODIFY 4
Text(I4MerchantTalk)
CHECK_MONEY
SVAL 0x1 0x384
BGE 0x1 0xc 0x1
Text(I4NoMoneyHa)
ENUF 0x15
GOTO 0x2
LABEL 0x1
SystemText(I4TomeBuy)
IfNoSelectedGoto(0x3)
Text(I4TomePurchased)
SVAL 0x3 0x384
TAKEMONEY
GiveItem(Mazandyne,Blair)
GOTO 0x2
LABEL 0x3
Text(I4TomeDenied)
ENUF 0x11
LABEL 0x2
EndEvent

I5ReadyToEnd:
EVBIT_MODIFY 4
Text(FillerDialogue)
SystemText(I1LeavePrompt)
IfNoSelectedGoto(0x2)
CALL EndingScene
GOTO 0x1
LABEL 0x2
Text(FillerDialogue)
LABEL 0x1
EndEvent

MESSAGE Events end at offset CURRENTOFFSET