//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

EventPointerTable(EventsI2,ThisChapter)


ALIGN 4

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN I2Units I2Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
CharacterEvent(0xa,I2Hyperius,Blair,Hyperius)
CharacterEvent(0xb,I2Alexis,Blair,Alexis)
CharacterEvent(0xc,I2Charley,Blair,Charley)
CharacterEvent(0xd,I2ManfredWilhelm,Blair,Manfred)
CharacterEvent(0xd,I2ManfredWilhelm,Blair,Wilhelm)
CharacterEvent(0xe,I2Merchant,Blair,Merchant)
CharacterEvent(0xf,I2Cit,Blair,Citizen)
CharacterEvent(0x10,I2Cit2,Blair,Citizen2)
CharacterEvent(0x0,I2ReadyToEnd,Blair,Cid)
END_MAIN

LocationBasedEvents:
Village(0x21,I2NorthVillage,6,2)
Village(0x22,I2SouthVillage,15,12)
Village(0x23,I2MidVillage,1,5)
House(0x24,I2Inn,18,18)
Armory(I2Armory,12,1)
Vendor(I2Vendor,8,7)
Armory(I2Armory2,5,8)
Vendor(I2Vendor2,10,13)
END_MAIN

ALIGN 4
I2Armory:
SHLI IronSword SteelSword Lancereaver Zanbato IronLance SteelLance Axereaver Horseslayer Javelin
BYTE 0x0 0x0
ALIGN 4
I2Armory2:
SHLI IronAxe SteelAxe Swordreaver Halberd HandAxe IronBow SteelBow ShiningBow
BYTE 0x0 0x0
ALIGN 4
I2Vendor:
SHLI Fire Thunder Elfire Lightning Shine Divine Flux Nightfall
BYTE 0x0 0x0
ALIGN 4
I2Vendor2:
SHLI Heal Mend Restore Barrier Vulnerary Elixir PureWater DoorKey ChestKey
BYTE 0x0 0x0
ALIGN 4

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4

I2Units:
UNIT Blair BlairLord 0x0 Level(1,Ally,0) [10,15] 0x0 0x0 0x0 0x0 [Fire, Awaken, Vulnerary] NoAI
UNIT

I2NPC:
UNIT Cid Berserker 0x0 Level(9,NPC,0) [25,1] 0x0 0x0 0x0 0x0 [Frypan,Elixir] NeverMoveAI
UNIT Wilhelm Soldier 0x0 Level(16,NPC,0) [0,9] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Manfred DarkM 0x0 Level(18,NPC,0) [0,7] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Charley SniperF 0x0 Level(2,NPC,0) [6,16] 0x0 0x0 0x0 0x0 [SilverBow,Greatbow,Elixir] NeverMoveAI
UNIT Alexis AxeArm 0x0 Level(8,NPC,0) [8,11] 0x0 0x0 0x0 0x0 [IronAxe, HandAxe, Vulnerary] NeverMoveAI
UNIT Hyperius SwdGen 0x0 Level(12,NPC,0) [6,9] 0x0 0x0 0x0 0x0 [SilverSword, IronSword, Vulnerary] NeverMoveAI
UNIT Merchant Merc 0x0 Level(19,NPC,1) [21,13] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Citizen CivilM 0x0 Level(1,NPC,1) [21,17] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Citizen2 CivilM 0x0 Level(1,NPC,1) [10,0] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT ElbaCor MageF 0x0 Level(14,NPC,1) [9,20] 0x0 0x0 0x0 0x0 [Elfire,Elixir] NeverMoveAI
UNIT ElbaCor LightM 0x0 Level(18,NPC,1) [4,21] 0x0 0x0 0x0 0x0 [Smite,Elixir] NeverMoveAI
UNIT ElbaCor Falco 0x0 Level(17,NPC,1) [15,15] 0x0 0x0 0x0 0x0 [GildedLance,Elixir] NeverMoveAI
UNIT

ALIGN 4

BeginningScene:
EnableFreeMovement
LOAD1 0x0 I2Units
LOAD1 0x0 I2NPC
ENUN
MUSC NarrationTheme
Narration(MapCG,Base2_Narration)
CleanTextNoMap(PortCG,I2_Opening)
GotoPrepScreenNoBaseCheck
ENDA

EndingScene:
DisableFreeMovement
EVBIT_MODIFY 0
Text(I2ReleaseMe)
MoveToChapter(Chapter9)
ENDA

I2Merchant:
EVBIT_MODIFY 4
Text(I2MerchantTalk)
CHECK_MONEY
SVAL 0x1 0x693
BGE 0x1 0xc 0x1
Text(I2NoMoneyHa)
ENUF 0xe
GOTO 0x2
LABEL 0x1
SystemText(I2SkillBuy)
IfNoSelectedGoto(0x3)
Text(I2SkillPurchased)
SVAL 0x3 0x693
TAKEMONEY
GiveItemWithDurability(SkillScroll,LightWeightID,Blair)
GOTO 0x2
LABEL 0x3
Text(I2SkillDenied)
ENUF 0xe
LABEL 0x2
EndEvent

I2Hyperius:
Text(I2HyperiusTalk)
EndEvent

I2Alexis:
Text(I2AlexisTalk)
GiveItem(AnkleWeight,Blair)
EndEvent

I2Charley:
Text(I2CharleyTalk)
EndEvent

I2ManfredWilhelm:
Text(I2ManfredWilhelmTalk)
EndEvent

I2Cit:
Text(I2CitTalk)
EndEvent

I2Cit2:
Text(I2Cit2Talk)
EndEvent

I2ReadyToEnd:
EVBIT_MODIFY 4
Text(I2ConfirmLeave)
SystemText(I1LeavePrompt)
IfNoSelectedGoto(0x2)
CALL EndingScene
GOTO 0x1
LABEL 0x2
Text(I2NotYet)
LABEL 0x1
EndEvent

I2MidVillage:
VillageEventItem(I2MidText, HouseCG, MiniGem)

I2NorthVillage:
HouseEvent(I2NorthText, HouseCG)

I2SouthVillage:
HouseEvent(I2SouthText, House2CG)

I2Inn:
HouseEvent(I2InnText, House2CG)

MESSAGE Events end at offset CURRENTOFFSET