//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

EventPointerTable(EventsI9,ThisChapter)


ALIGN 4

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN I9Units I9Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
CharacterEvent(0x15,I9Merchant,Blair,Merchant)
CharacterEvent(0x0,I9ReadyToEnd,Blair,Zeke)
CharacterEvent(0xf,I9Cit,Blair,Citizen)
CharacterEvent(0x10,I9Cit2,Blair,Citizen2)
END_MAIN

LocationBasedEvents:
//copied from I2 currently ha
House(0x21,I9NorthVillage,18,1)
Village(0x22,I9SouthVillage,9,13)
Village(0x23,I9MidVillage,4,10)
House(0x24,I9Inn,9,6)
Armory(I9Armory,15,2)
Vendor(I9Vendor,3,2)
Armory(I9Armory2,13,12)
Vendor(I9Vendor2,5,4)
END_MAIN

ALIGN 4
I9Armory:
SHLI SilverSword GildedSword BraveSword Lancereaver Zanbato Armorslayer Wyrmslayer SilverLance GildedLance BraveLance Spear Axereaver Horseslayer Shatterspear Dragonpike
BYTE 0x0 0x0
ALIGN 4
I9Armory2:
SHLI SilverAxe GildedAxe BraveAxe Tomahawk Swordreaver Hammer Halberd Dracoaxe SilverBow GildedBow BraveBow Longbow Greatbow
BYTE 0x0 0x0
ALIGN 4
I9Vendor:
SHLI Elfire Fimbulvetr Flare Calibur Inversion Divine Aura Radiance Pierce Banish Nightfall Fenrir Nocturne Knoggelvi Corruption
BYTE 0x0 0x0
ALIGN 4
I9Vendor2:
SHLI Mend Physic Recover Barrier Yoink Runeward Implant Vulnerary Elixir PureWater MasterSeal
BYTE 0x0 0x0
ALIGN 4

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4

I9Units:
UNIT Blair BlairLord 0x0 Level(1,Ally,0) [21,7] 0x0 0x0 0x0 0x0 [Fire, Awaken, Vulnerary] NoAI
UNIT Blair BlairLord 0x0 Level(1,Ally,0) [22,7] 0x0 0x0 0x0 0x0 [Fire, Awaken, Vulnerary] NoAI
UNIT

I9NPC:
UNIT Merchant BowGen 0x0 Level(12,NPC,1) [20,14] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Citizen CitizenM 0x0 Level(1,NPC,1) [2,5] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Citizen2 CitizenF 0x0 Level(1,NPC,1) [13,10] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Holanis AxeArm 0x0 Level(20,NPC,1) [17,5] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Holanis AxeArm 0x0 Level(20,NPC,1) [17,9] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Holanis MageF 0x0 Level(20,NPC,1) [6,14] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Holanis Archer 0x0 Level(20,NPC,1) [16,0] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT

TonnaminJoin:
UNIT Tonnamin AxeGen 0x0 Level(7,Ally,0) [10,8] 0x0 0x0 0x0 0x0 [Hurlbat] NoAI
UNIT

ALIGN 4

BeginningScene:
EnableFreeMovement
MUSC NarrationTheme5
Narration(Map2CG,Base9_Narration)
RandomRainSetter
CleanTextNoMap(EastHolanisCG,I9Opening)
LOAD1 0x0 I9Units
ENUN
LOAD1 0x0 I9NPC
ENUN
ResetViridianConvoy
GotoPrepScreenNoBaseCheck
ENDA

EndingScene:
DisableFreeMovement
EVBIT_MODIFY 3
Text(I9ReleaseMe)
MUSC AmbientWind
CleanTextNoMap(EastHolanisCG,I9Ending)
ResetBaseFlags
MoveToChapter(Chapter23)
ENDA

I9ReadyToEnd:
EVBIT_MODIFY 4
Text(I9ConfirmLeave)
SystemText(I1LeavePrompt)
IfNoSelectedGoto(0x2)
CALL EndingScene
GOTO 0x1
LABEL 0x2
Text(I9NotYet)
LABEL 0x1
EndEvent

I9NorthVillage:
EVBIT_MODIFY 3
SetBackground(House2CG)
CheckForItem(ArkhinalHandle)
BEQ 0x1 0xc 0x0
Text(I9HandleAsk)//notices handle
SystemText(I9HandlePrompt)//prompt to turn in handle
IfNoSelectedGoto(0x3)
AnnihilateItem(ArkhinalHandle)
Text(I9ReforgeArkhinal)
REMA
SetBackground(BlankCG)
GiveItem(Arkhinal,Blair)
FADI 16
CLEAN
FADU 16
TILECHANGE 0x1
GOTO 0x2
LABEL 0x1
Text(I9WeaponryTalk)//no handle
FADI 16
CLEAN
FADU 16
TILECHANGE 0x1
GOTO 0x2
LABEL 0x3
Text(I9Refusal)//refuse to give up handle
ENUF 0x21
FADI 16
CLEAN
FADU 16
LABEL 0x2
EndEvent

I9Cit:
Text(I9CitTalk)
EndEvent

I9Cit2:
Text(I9Cit2Talk)
EndEvent


I9MidVillage:
HouseEvent(I9MidHouseTalk, HouseCG)

I9SouthVillage:
HouseEvent(I9SouthHouseTalk, HouseCG)

I9Merchant:
//weird guy sells torch staff for 19998 gold
EVBIT_MODIFY 4
Text(I9MerchantTalk)
CHECK_MONEY
SVAL 0x1 0x4E1E
BGE 0x1 0xc 0x1
Text(I9NoMoneyHa)
ENUF 0x15
GOTO 0x2
LABEL 0x1
SystemText(I4BuyingtonPrompt)
IfNoSelectedGoto(0x3)
Text(I9TreasurePurchased)
SVAL 0x3 0x4E1E
TAKEMONEY
GiveItem(TorchStaff,Blair)
GOTO 0x2
LABEL 0x3
Text(I9TreasureDenied)
ENUF 0x15
LABEL 0x2
EndEvent

//check for other inn flags
I9Inn:
MUSI
CHECK_EVENTID TavernI1Flag
BEQ 0x1 0xc 0x0
CHECK_EVENTID TavernI2Flag
BEQ 0x1 0xc 0x0
CHECK_EVENTID TavernI4Flag
BEQ 0x1 0xc 0x0
CHECK_EVENTID TavernI5Flag
BEQ 0x1 0xc 0x0
CHECK_EVENTID TavernI7Flag
BEQ 0x1 0xc 0x0
SetBackground(InnCG)
Text(I9InnTextAlt)
FADI 16
Reinforcements(TonnaminJoin)
CLEAN
FADU 16
GOTO 0x2
LABEL 0x1
CleanText(InnCG,I9InnText)
LABEL 0x2
MUNO
EndEvent

//MESSAGE Events end at offset CURRENTOFFSET