//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

EventPointerTable(EventsI4,ThisChapter)


ALIGN 4

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN I4Units I4Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
CharacterEvent(0x0,I4ReadyToEnd,Blair,Vladmir)
CharacterEvent(0x10,I4Laronde,Blair,Laronde)
CharacterEvent(0x11,I4Athena,Blair,Athena)
CharacterEvent(0x12,I4Callum,Blair,Callum)
CharacterEvent(0x13,I4Leila,Blair,Leila)
CharacterEvent(0x14,I4Liam,Blair,Liam)
CharacterEvent(0x15,I4Merchant,Blair,Merchant)
CharacterEvent(0x16,I4Cit,Blair,Citizen)
CharacterEvent(0x17,I4Cit2,Blair,Citizen2)
END_MAIN

LocationBasedEvents:
Armory(I4Armory,1,24)
Vendor(I4Vendor,11,24)
House(0xa,I4VerySecretSpace,2,9)
END_MAIN

ALIGN 4
I4Armory:
SHLI SteelSword Armorslayer KillingEdge SteelLance Shatterspear KillerLance Javelin SteelAxe Hammer KillerAxe HandAxe SteelBow KillerBow Longbow
BYTE 0x0 0x0
ALIGN 4
I4Vendor:
SHLI Thunder Elfire Shine Divine Nightfall Nosferatu Mend Recover Yoink Vulnerary Elixir PureWater
BYTE 0x0 0x0
ALIGN 4

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4

I4Units:
UNIT Blair BlairLord 0x0 Level(1,Ally,0) [5,1] 0x0 0x0 0x0 0x0 [Fire, Awaken, Vulnerary] NoAI
UNIT

I4NPC:
UNIT Vladmir Bishop 0x0 Level(15,NPC,0) [6,32] 0x0 0x0 0x0 0x0 [BookOfOpistus] NeverMoveAI
UNIT Laronde Halberdier 0x0 Level(4,NPC,1) [12,1] 0x0 0x0 0x0 0x0 [Spear,Nuboko,Elixir] NeverMoveAI
UNIT Athena AxeWyv 0x0 Level(14,NPC,0) [5,22] 0x0 0x0 0x0 0x0 [Hammer,HandAxe,MasterSeal] NeverMoveAI
UNIT Callum SwdPal 0x0 Level(11,NPC,0) [12,26] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Leila SwdPeg 0x0 Level(13,NPC,0) [17,24] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Merchant SorcF 0x0 Level(8,NPC,1) [8,20] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Citizen CivilM 0x0 Level(1,NPC,1) [0,35] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Citizen2 CivilM 0x0 Level(1,NPC,1) [10,30] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Liam Hero 0x0 Level(16,NPC,1) [17,9] 0x0 0x0 0x0 0x0 [Phalanotis] NeverMoveAI
UNIT Opistia LightM 0x0 Level(18,NPC,1) [5,7] 0x0 0x0 0x0 0x0 [Aura,Elixir] NeverMoveAI
UNIT Opistia LightM 0x0 Level(19,NPC,1) [12,7] 0x0 0x0 0x0 0x0 [Aura,Elixir] NeverMoveAI
UNIT Opistia Priest 0x0 Level(17,NPC,1) [4,12] 0x0 0x0 0x0 0x0 [Physic,Elixir] NeverMoveAI
UNIT Opistia Cleric 0x0 Level(14,NPC,1) [13,12] 0x0 0x0 0x0 0x0 [Physic,Elixir] NeverMoveAI
UNIT Opistia Bishop 0x0 Level(4,NPC,1) [6,5] 0x0 0x0 0x0 0x0 [Aura,Fortify,Elixir] NeverMoveAI
UNIT Opistia BishopF 0x0 Level(7,NPC,1) [4,1] 0x0 0x0 0x0 0x0 [Aura,Sleep,Elixir] NeverMoveAI
UNIT Opistia Bishop 0x0 Level(9,NPC,1) [11,5] 0x0 0x0 0x0 0x0 [Aura,Again,Elixir] NeverMoveAI
UNIT Opistia BishopF 0x0 Level(12,NPC,1) [13,1] 0x0 0x0 0x0 0x0 [Aura,Thief,Elixir] NeverMoveAI
UNIT

I4VerySpecialUnit:
UNIT Addington Automaton 0x0 Level(1,Ally,0) [1,9] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT

ALIGN 4

BeginningScene:
EnableFreeMovement
LOAD1 0x0 I4Units
LOAD1 0x0 I4NPC
ENUN
MUSC NarrationTheme2
Narration(Base4_Narration)
CleanTextNoMap(HallCG,I4_Opening)
GotoPrepScreen
ENDA

EndingScene:
DisableFreeMovement
Text(I4ReleaseMe)
MoveToChapter(Chapter13)
ENDA

I4Laronde:
Text(I4LarondeTalk)
EndEvent

I4Leila:
Text(FillerDialogue)
EndEvent

I4Athena:
Text(FillerDialogue)
EndEvent

I4Callum:
Text(FillerDialogue)
EndEvent

I4Cit:
Text(FillerDialogue)
EndEvent

I4Cit2:
Text(FillerDialogue)
EndEvent

I4Merchant:
EVBIT_MODIFY 4
Text(I2MerchantTalk)
CHECK_MONEY
SVAL 0x1 0x693
BGE 0x1 0xc 0x1
Text(I2NoMoneyHa)
ENUF 0x15
GOTO 0x2
LABEL 0x1
SystemText(I2SkillBuy)
IfNoSelectedGoto(0x3)
Text(I2SkillPurchased)
SVAL 0x3 0x693
TAKEMONEY
GiveItemWithDurability(SkillScroll,LightWeightID,Blair)
GOTO 0x2
LABEL 0x3
Text(I2SkillDenied)
ENUF 0x15
LABEL 0x2
EndEvent

I4Liam:
EVBIT_MODIFY 4
Text(I4LiamTalk)
SystemText(I4LiamChoice)
IfNoSelectedGoto(0x3)
Text(I4LiamYes)
ENUT LiamAcceptedFlag
GOTO 0x2
LABEL 0x3
Text(I4LiamNo)
ENUF 0x14
LABEL 0x2
EndEvent

I4ReadyToEnd:
EVBIT_MODIFY 4
Text(I4ConfirmLeave)
SystemText(I1LeavePrompt)
IfNoSelectedGoto(0x2)
CALL EndingScene
GOTO 0x1
LABEL 0x2
Text(I4NotYet)
LABEL 0x1
EndEvent

I4VerySecretSpace:
EVBIT_MODIFY 4
MUSC S_CampIchigaya
SetBackground(HallCG)
Text(I4Buyington)
CHECK_MONEY
SVAL 0x1 0x1E60
BGE 0x1 0xc 0x1
Text(I4BuyingtonBroke)
ENUF 0xa
FADI 60
CLEAN
FADU 60
GOTO 0x2
LABEL 0x1
SystemText(I4BuyingtonPrompt)
IfNoSelectedGoto(0x3)
Text(I4BuyingtonBought)
SVAL 0x3 0x1E60
TAKEMONEY
FADI 60
Reinforcements(I4VerySpecialUnit)
CLEAN
FADU 60
GOTO 0x2
LABEL 0x3
Text(I4BuyingtonNo)
ENUF 0xa
FADI 60
CLEAN
FADU 60
LABEL 0x2
EndEvent
MESSAGE Events end at offset CURRENTOFFSET