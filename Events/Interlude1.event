//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

EventPointerTable(EventsI1,ThisChapter)


ALIGN 4

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData2
POIN I1Units I1Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0x0,I1Open,1)
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
CharacterEvent(0xa,I1Peter,Blair,Peter)
CharacterEvent(0xb,I1Ptolemy,Blair,Ptolemy)
CharacterEvent(0xc,I1Estelle,Blair,Estelle)
CharacterEvent(0xd,I1Rift,Blair,Rift)
CharacterEvent(0xe,I1Merchant,Blair,Merchant)
CharacterEvent(0xf,I1Cit,Blair,Citizen)
CharacterEvent(0x10,I1Cit2,Blair,Citizen2)
CharacterEvent(0x0,I1ReadyToEnd,Blair,IntArin)
END_MAIN

LocationBasedEvents:
Armory(I1Armory,10,10)
Vendor(I1Vendor,13,10)
House(0x21,I1West,5,14)
House(0x22,I1East1,19,15)
House(TavernI1Flag,I1East2,22,15)
END_MAIN

ALIGN 4
I1Armory:
SHLI IronSword SteelSword Lancereaver IronLance SteelLance Axereaver Javelin IronAxe SteelAxe Swordreaver HandAxe IronBow SteelBow
BYTE 0x0 0x0
ALIGN 4

I1Vendor:
SHLI Fire Thunder Lightning Shine Flux Nightfall Heal Mend Restore Vulnerary PureWater
BYTE 0x0 0x0
ALIGN 4

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
TelliusTorchOn(21,7,1)
TelliusTorchOn(23,7,1)
TelliusTorchOn(21,9,1)
TelliusTorchOn(23,9,1)
ENDTRAP
TrapData2:
ENDTRAP

ALIGN 4

I1Units:
UNIT Blair BlairLord 0x0 Level(1,Ally,0) [12,12] 0x0 0x0 0x0 0x0 [Fire, Awaken, Vulnerary] NoAI
UNIT

I1NPC:
UNIT Ptolemy BowArm 0x0 Level(14,NPC,0) [2,12] 0x0 0x0 0x0 0x0 [BeecurveBow,Beelista,HoneyCharm] NeverMoveAI
UNIT Peter WyvLord 0x0 Level(8,NPC,0) [19,3] 0x0 0x0 0x0 0x0 [Tomahawk,Dragonpike,Elixir] NeverMoveAI
UNIT Rift SwMast 0x0 Level(6,NPC,0) [17,16] 0x0 0x0 0x0 0x0 [FlameSword,SteelSword,Vulnerary] NeverMoveAI
UNIT Estelle BowCavF 0x0 Level(13,NPC,0) [21,16] 0x0 0x0 0x0 0x0 [IronBow,SteelBow,Longbow,Vulnerary] NeverMoveAI
UNIT Merchant Sage 0x0 Level(5,NPC,1) [6,0] 0x0 0x0 0x0 0x0 [Yoink] NeverMoveAI
UNIT Citizen CitizenM 0x0 Level(1,NPC,1) [13,1] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Citizen2 CitizenM 0x0 Level(1,NPC,1) [16,13] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Mevion MercF 0x0 Level(7,NPC,1) [25,7] 0x0 0x0 0x0 0x0 [SilverSword,Elixir] NeverMoveAI
UNIT Mevion LncCav 0x0 Level(8,NPC,1) [3,16] 0x0 0x0 0x0 0x0 [SilverLance,Elixir] NeverMoveAI
UNIT Mevion Halberdier 0x0 Level(2,NPC,1) [23,5] 0x0 0x0 0x0 0x0 [SilverLance,Elixir] NeverMoveAI
UNIT

//probably could've made her blue but i didn't feel like branching with her death lol
I1Arin:
UNIT IntArin Trickster 0x0 Level(3,NPC,0) [12,13] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT

I1ElbaCorians:
UNIT Rachael LncPeg 0x0 Level(4,NPC,0) [13,17] 0x0 0x0 0x1 REDA12R12 [IronLance,Javelin,EnergyRing] NoAI
UNIT Cassius AxeCav 0x0 Level(5,NPC,0) [14,17] 0x0 0x0 0x1 REDA13R13 [SteelAxe,IronAxe,Vulnerary] NoAI
UNIT

ALIGN 4

BeginningScene:
EnableFreeMovement
LOAD1 0x0 I1Units
LOAD1 0x0 I1NPC
ENUN
MUSC NarrationTheme
Narration(MapCG,Base1_Narration)
RandomRainSetter
MUSC AmbientBirds
CleanTextNoMap(StreetCG,I1_Opening)
GotoPrepScreenNoBaseCheck
ENDA

EndingScene:
DisableFreeMovement
EVBIT_F 9
TEXTSTART
TEXTSHOW I1ReleaseMe
TEXTEND
CAM1 [11,17]
LOAD1 0x0 I1ElbaCorians
ENUN
TEXTCONT
TEXTEND
MUSC RecruitmentTheme
TurnAlly(Rachael)
TurnAlly(Cassius)
TEXTCONT
TEXTEND
MoveToChapter(Chapter6)
ENDA

I1Open:
LOAD1 0x0 I1Arin
ENUN
Text(IntExplanation)
SET_ACTIVE Blair
EndEvent

I1Peter:
Text(I1PeterTalk)
EndEvent

I1Ptolemy:
Text(I1PtolemyTalk)
GiveItem(HoneyCharm,Blair)
CHECK_EVENTID 0xef
BEQ 0x1 0xc 0x0
CHECK_EVENTID 0xee
BEQ 0x1 0xc 0x0
SystemText(GrowthModDisclaimer)
LABEL 0x1
EndEvent

I1Estelle:
Text(I1EstelleTalk)
EndEvent

I1Rift:
Text(I1RiftTalk)
EndEvent

I1Merchant:
EVBIT_MODIFY 4
Text(I1MerchantTalk)
CHECK_MONEY
SVAL 0x1 0x5DC
BGE 0x1 0xc 0x1
Text(I1NoMoneyHa)
ENUF 0xe
GOTO 0x2
LABEL 0x1
SystemText(I1StaffBuy)
IfNoSelectedGoto(0x3)
Text(I1StaffPurchased)
SVAL 0x3 0x5dc
TAKEMONEY
GiveItem(Yoink,Blair)
GOTO 0x2
LABEL 0x3
Text(I1StaffDenied)
ENUF 0xe
LABEL 0x2
EndEvent

I1Cit:
Text(I1CitTalk)
EndEvent

I1Cit2:
Text(I1Cit2Talk)
EndEvent

I1West:
HouseEvent(I1House1,House2CG)

I1East1:
HouseEvent(I1House2,HouseCG)

I1East2:
HouseEvent(I1House3,InnCG)

I1ReadyToEnd:
EVBIT_MODIFY 4
Text(I1ConfirmLeave)
SystemText(I1LeavePrompt)
IfNoSelectedGoto(0x2)
CALL EndingScene
GOTO 0x1
LABEL 0x2
Text(I1NotYet)
LABEL 0x1
EndEvent

//MESSAGE Events end at offset CURRENTOFFSET