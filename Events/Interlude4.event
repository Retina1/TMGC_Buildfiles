//Made by Camtech075 of SerenesForest
//Modified by Nintenlord

#include EAstdlib.event

EventPointerTable(EventsI4,ThisChapter)


ALIGN 4

ThisChapter:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno Dunno Dunno
POIN Tutorial
POIN TrapData TrapData
POIN I4Units I4Units
POIN $0 $0 $0 $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
END_MAIN
ALIGN 32 //For some reason, CharacterBasedEvents have to be 32-Aligned?

CharacterBasedEvents:
CharacterEvent(0x0,I4ReadyToEnd,Blair,Vladmir)
CharacterEvent(0x10,I4Laronde,Blair,Laronde)
CharacterEvent(0x11,I4Athena,Blair,Athena)
CharacterEvent(0x12,I4DirkEliza,Blair,Eliza)
CharacterEvent(0x12,I4DirkEliza,Blair,Dirk)
CharacterEvent(0x13,I4Leila,Blair,Leila)
CharacterEvent(0x14,I4Liam,Blair,Liam)
CharacterEvent(0x15,I4Merchant,Blair,Merchant3)
CharacterEvent(0x16,I4Cit,Blair,Citizen)
CharacterEvent(0x17,I4Cit2,Blair,Citizen2)
END_MAIN

LocationBasedEvents:
Armory(I4Armory,1,24)
Vendor(I4Vendor,11,24)
House(0xa,I4VerySecretSpace,2,9)
Village(0x21,I4LeftVillage,2,33)
Village(0x22,I4CenterVillage,10,32)
Village(0x23,I4RightVillage,16,29)
Village(TavernI4Flag,I4Inn,3,29)
House(0x0,CallInterludeSupply,14,32)
END_MAIN

ALIGN 4
I4Armory:
SHLI SteelSword Armorslayer KillingEdge SteelLance Shatterspear KillerLance Javelin SteelAxe Hammer KillerAxe HandAxe SteelBow KillerBow Longbow
BYTE 0x0 0x0
ALIGN 4
I4Vendor:
SHLI GoddessIcon Thunder Elfire Shine Divine Nightfall Nosferatu Mend Recover Yoink Vulnerary Elixir PureWater
BYTE 0x0 0x0
ALIGN 4

MiscBasedEvents:
CauseGameOverIfLordDies
END_MAIN

Dunno:
END_MAIN

Tutorial:
END_MAIN

TrapData:
ENDTRAP

ALIGN 4

I4Units:
UNIT Blair BlairLord 0x0 Level(1,Ally,0) [5,1] 0x0 0x0 0x0 0x0 [Fire, Awaken, Vulnerary] NoAI
UNIT

I4NPC:
UNIT Vladmir Bishop 0x0 Level(15,NPC,0) [6,32] 0x0 0x0 0x0 0x0 [BookOfOpistus] NeverMoveAI
UNIT Laronde Halberdier 0x0 Level(4,NPC,1) [12,1] 0x0 0x0 0x0 0x0 [Spear,Nuboko,Elixir] NeverMoveAI
UNIT Athena AxeWyv 0x0 Level(14,NPC,0) [5,22] 0x0 0x0 0x0 0x0 [Hammer,HandAxe,MasterSeal] NeverMoveAI
UNIT Eliza Cleric 0x0 Level(12,NPC,0) [12,26] 0x0 0x0 0x0 0x0 [Recover, Physic, Rescue] NeverMoveAI
UNIT Dirk Berserker 0x0 Level(7,NPC,0) [12,27] 0x0 0x0 0x0 0x0 [Swordslayer,HereticAxe] NeverMoveAI
UNIT Leila SwdPeg 0x0 Level(13,NPC,0) [17,24] 0x0 0x0 0x0 0x0 [Rapier, SteelSword, AngelicRobe] NeverMoveAI
UNIT Merchant3 SorcF 0x0 Level(4,NPC,0) [8,20] 0x0 0x0 0x0 0x0 [Mazandyne,Fenrir,Hell,Sleep] NeverMoveAI
UNIT Citizen CitizenM 0x0 Level(1,NPC,1) [0,35] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Citizen2 CitizenM 0x0 Level(1,NPC,1) [10,30] 0x0 0x0 0x0 0x0 [0x0] NeverMoveAI
UNIT Liam Hero 0x0 Level(16,NPC,1) [17,9] 0x0 0x0 0x0 0x0 [Phalanotis] NeverMoveAI
UNIT Opistia LightM 0x0 Level(18,NPC,1) [5,7] 0x0 0x0 0x0 0x0 [Aura,Elixir] NeverMoveAI
UNIT Opistia LightM 0x0 Level(19,NPC,1) [12,7] 0x0 0x0 0x0 0x0 [Aura,Elixir] NeverMoveAI
UNIT Opistia Priest 0x0 Level(17,NPC,1) [4,12] 0x0 0x0 0x0 0x0 [Physic,Elixir] NeverMoveAI
UNIT Opistia Cleric 0x0 Level(14,NPC,1) [13,12] 0x0 0x0 0x0 0x0 [Physic,Elixir] NeverMoveAI
UNIT Opistia Bishop 0x0 Level(4,NPC,1) [6,5] 0x0 0x0 0x0 0x0 [Aura,Fortify,Elixir] NeverMoveAI
UNIT Opistia BishopF 0x0 Level(7,NPC,1) [4,1] 0x0 0x0 0x0 0x0 [Aura,Sleep,Elixir] NeverMoveAI
UNIT Opistia Bishop 0x0 Level(9,NPC,1) [11,5] 0x0 0x0 0x0 0x0 [Aura,Again,Elixir] NeverMoveAI
UNIT Opistia BishopF 0x0 Level(12,NPC,1) [13,1] 0x0 0x0 0x0 0x0 [Aura,ThiefStaff,Elixir] NeverMoveAI
UNIT

I4VerySpecialUnit:
UNIT Addington Automaton 0x0 Level(1,Ally,0) [1,9] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT

ALIGN 4

BeginningScene:
EnableFreeMovement
LOAD1 0x0 I4Units
LOAD1 0x0 I4NPC
ENUN
MUSC NarrationTheme2
Narration(MapCG,Base4_Narration)
RandomRainSetter
MUSC AmbientBirds
CleanTextNoMap(HallCG,I4_Opening)
GotoPrepScreenNoBaseCheck
ENDA

EndingScene:
DisableFreeMovement
EVBIT_MODIFY 3
Text(I4ReleaseMe)
MoveToChapter(Chapter13)
ENDA

I4Laronde:
Text(I4LarondeTalk)
EndEvent

I4Leila:
TEXTSTART
TEXTSHOW I4LeilaTalk
TEXTEND
SOUN 0xa6
DISA_IF Leila
DISA Leila
ENUN
TEXTCONT
TEXTEND
REMA
EndEvent

I4Athena:
Text(I4AthenaTalk)
EndEvent

I4DirkEliza:
Text(I4DirkElizaTalk)
EndEvent

I4Cit:
Text(I4Cit1Talk)
EndEvent

I4Cit2:
Text(I4Cit2Talk)
EndEvent

I4LeftVillage:
VillageEventItem(I4LeftTalk, HouseCG, Pierce)
I4CenterVillage:
HouseEvent(I4CenterTalk, HouseCG)
I4RightVillage:
HouseEvent(I4RightTalk, HouseCG)
//how did i forget my own running gag
I4Inn:
HouseEvent(I4InnText, InnCG)

I4Merchant:
EVBIT_MODIFY 4
Text(I4MerchantTalk)
CHECK_MONEY
SVAL 0x1 0x384
BGE 0x1 0xc 0x1
Text(I4NoMoneyHa)
ENUF 0x15
GOTO 0x2
LABEL 0x1
SystemText(I4TomeBuy)
IfNoSelectedGoto(0x3)
Text(I4TomePurchased)
SVAL 0x3 0x384
TAKEMONEY
GiveItem(Mazandyne,Blair)
GOTO 0x2
LABEL 0x3
Text(I4TomeDenied)
ENUF 0x15
LABEL 0x2
EndEvent

I4Liam:
EVBIT_MODIFY 4
Text(I4LiamTalk)
SystemText(I4LiamChoice)
IfNoSelectedGoto(0x3)
Text(I4LiamYes)
ENUT LiamAcceptedFlag
GOTO 0x2
LABEL 0x3
Text(I4LiamNo)
ENUF 0x14
LABEL 0x2
EndEvent

I4ReadyToEnd:
EVBIT_MODIFY 4
Text(I4ConfirmLeave)
SystemText(I1LeavePrompt)
IfNoSelectedGoto(0x2)
CALL EndingScene
GOTO 0x1
LABEL 0x2
Text(I4NotYet)
LABEL 0x1
EndEvent

I4VerySecretSpace:
EVBIT_MODIFY 4
MUSC S_CampIchigaya
SetBackground(HallCG)
Text(I4Buyington)
CHECK_MONEY
SVAL 0x1 0x1E60
BGE 0x1 0xc 0x1
Text(I4BuyingtonBroke)
ENUF 0xa
FADI 16
CLEAN
FADU 16
GOTO 0x2
LABEL 0x1
SystemText(I4BuyingtonPrompt)
IfNoSelectedGoto(0x3)
Text(I4BuyingtonBought)
SVAL 0x3 0x1E60
TAKEMONEY
FADI 16
Reinforcements(I4VerySpecialUnit)
CLEAN
FADU 16
GOTO 0x2
LABEL 0x3
Text(I4BuyingtonNo)
ENUF 0xa
FADI 16
CLEAN
FADU 16
LABEL 0x2
EndEvent
//MESSAGE Events end at offset CURRENTOFFSET